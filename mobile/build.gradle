buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.3'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'crashlytics'
apply plugin: 'monkey'

def onCI = "CI".equals(System.getenv("SYSTEM_TYPE"))

monkey {
    failOnFailure = true
    eventCount = 1000000
    seed = new Random().nextInt()
    teamCityLog = onCI
}

// Manifest Information
def versionMajor = 0
def versionMinor = 2
def versionPatch = 0

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.truckmuncher.truckmuncher"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        resConfigs "en"
        resConfigs "nodpi", "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        buildConfigField "String", "TWITTER_API_KEY", "\"${TRUCK_MUNCHER_TWITTER_API_KEY}\""
        buildConfigField "String", "TWITTER_API_SECRET", "\"${TRUCK_MUNCHER_TWITTER_API_SECRET}\""
    }

    signingConfigs {
        release {
            storeFile file(TRUCK_MUNCHER_RELEASE_STORE_FILE)
            storePassword TRUCK_MUNCHER_RELEASE_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_RELEASE_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file(TRUCK_MUNCHER_DEBUG_STORE_FILE)
            storePassword TRUCK_MUNCHER_DEBUG_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_DEBUG_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_DEBUG_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            manifestPlaceholders = [googleMapsKey: ""]
            testCoverageEnabled false   // Incompatible with Dagger
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            manifestPlaceholders = [googleMapsKey: "AIzaSyCViPU_TIFcwHQUmPXY21EowkO2XtCFI9s"]
            testCoverageEnabled false   // Incompatible with Dagger
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        production {
            buildConfigField "String", "API_ENDPOINT", "\"https://api.truckmuncher.com:8443\""
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
        dev {
            buildConfigField "String", "API_ENDPOINT", "\"http://truckmuncher:8443\""
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.dev"
        }
        mock {
            buildConfigField "String", "API_ENDPOINT", "\"mock://\""
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.mock"
        }
    }

    jacoco {
        version = '0.7.2.201409121644'
    }

    dexOptions {
        // preDexing gives no advantage on CI since we do a clean build
        preDexLibraries = !onCI
    }

    lintOptions {
        enable 'IconExpectedSize', 'StopShip', 'TypographyQuotes'
        disable 'InvalidPackage'
        abortOnError true
        ignoreWarnings false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services-maps:6.5.87'
    compile 'com.google.android.gms:play-services-location:6.5.87'
    compile 'com.google.maps.android:android-maps-utils:0.3.3'
    compile project(':AndroidSocialNetworks')

    // Network and Data stack
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.retrofit:converter-wire:1.7.0'

    // Logging
    compile 'com.crashlytics.android:crashlytics:1.1.11'
    compile 'com.jakewharton.timber:timber:2.4.1'

    // UI Utils
    compile 'com.volkhart:android-util:1.0.0-SNAPSHOT@aar'
    compile 'com.squareup.phrase:phrase:1.0.3'
    compile 'com.jakewharton:butterknife:6.0.0'

    // UI Components
    compile 'com.twotoasters.SectionCursorAdapter:library:1.0.1'
    compile 'com.sothree.slidinguppanel:library:2.0.4'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile project(':minified-guava')

    debugCompile 'com.squareup.retrofit:retrofit-mock:1.7.0'

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger'
    }
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.0.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    androidTestCompile 'org.mockito:mockito-core:1.10.8'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testCompile project(':testlib')
    testCompile 'org.mockito:mockito-core:1.10.8'
}

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    jcenter()
}
