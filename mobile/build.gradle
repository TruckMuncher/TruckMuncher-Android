buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'crashlytics'
apply plugin: 'monkey'

def onCI = "CI".equals(System.getenv("SYSTEM_TYPE"))

monkey {
    failOnFailure = true
    eventCount = 1000000
    seed = new Random().nextInt()
    teamCityLog = onCI
}

// Manifest Information
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.truckmuncher.truckmuncher"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        resConfigs "en"
        resConfigs "nodpi", "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        buildConfigField "String", "TWITTER_API_KEY", "\"${TRUCK_MUNCHER_TWITTER_API_KEY}\""
        buildConfigField "String", "TWITTER_API_SECRET", "\"${TRUCK_MUNCHER_TWITTER_API_SECRET}\""
    }

    signingConfigs {
        release {
            storeFile file(TRUCK_MUNCHER_RELEASE_STORE_FILE)
            storePassword TRUCK_MUNCHER_RELEASE_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_RELEASE_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file(TRUCK_MUNCHER_DEBUG_STORE_FILE)
            storePassword TRUCK_MUNCHER_DEBUG_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_DEBUG_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_DEBUG_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
//            testCoverageEnabled true
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
//            testCoverageEnabled true
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        production {
            buildConfigField "String", "API_ENDPOINT", "\"https://api.truckmuncher.com:8443\""
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
        devMarius {
            buildConfigField "String", "API_ENDPOINT", "\"https://Marius-MBA:8443\""
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.dev"
        }
    }

    jacoco {
        version = '0.7.2.201409121644'
    }

    dexOptions {
        // preDexing gives no advantage on CI since we do a clean build
        preDexLibraries = !onCI
    }

    lintOptions {
        enable 'IconExpectedSize', 'StopShip', 'TypographyQuotes'
        disable 'InvalidPackage'
        abortOnError true
        ignoreWarnings false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
}

dependencies {
    wearApp project(':wear')
    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.google.android.gms:play-services:6.1.11'
    compile project(':AndroidSocialNetworks')

    // Network and Data stack
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.retrofit:retrofit:1.7.0'
    compile 'com.squareup.retrofit:converter-wire:1.7.0'
    compile 'com.squareup.wire:wire-runtime:1.5.2'

    // Logging
    compile 'com.crashlytics.android:crashlytics:1.1.11'
    compile 'com.jakewharton.timber:timber:2.4.1'

    // UI Utils
    compile 'com.volkhart:android-util:1.0.0-SNAPSHOT@aar'
    compile 'com.squareup.phrase:phrase:1.0.3'
    compile 'com.jakewharton:butterknife:5.1.2'

    // UI Components
    compile 'com.twotoasters.SectionCursorAdapter:library:1.0.1'

    debugCompile 'com.squareup.retrofit:retrofit-mock:1.7.0'


    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile 'com.jakewharton.espresso:espresso:1.1-r3'
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.0.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    androidTestCompile 'org.mockito:mockito-core:1.10.8'
    androidTestCompile fileTree(dir: 'testLibs', include: ['*.jar'])
}

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}
