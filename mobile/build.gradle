buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.15.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.6'
        classpath('com.github.triplet.gradle:play-publisher:1.0.2') {
            exclude group: 'com.google.guava'
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'hugo'
apply plugin: 'com.autoscout24.gradle.monkey'
apply plugin: 'android-apt'
apply plugin: 'errorprone'
apply plugin: 'play'

def onCI = "CI".equals(System.getenv("SYSTEM_TYPE"))

monkey {
    failOnFailure = true
    eventCount = 1000000
    seed = new Random().nextInt()
    teamCityLog = onCI
}

// Manifest Information
def versionMajor = 1
def versionMinor = 0

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.truckmuncher.app"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode versionMajor * 100 + versionMinor
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        resConfigs "en"
        resConfigs "nodpi", "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        buildConfigField "String", "TWITTER_API_KEY", "\"${TRUCK_MUNCHER_TWITTER_API_KEY}\""
        buildConfigField "String", "TWITTER_API_SECRET", "\"${TRUCK_MUNCHER_TWITTER_API_SECRET}\""
    }

    signingConfigs {
        release {
            storeFile file(TRUCK_MUNCHER_RELEASE_STORE_FILE)
            storePassword TRUCK_MUNCHER_RELEASE_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_RELEASE_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file(TRUCK_MUNCHER_DEBUG_STORE_FILE)
            storePassword TRUCK_MUNCHER_DEBUG_STORE_PASSWORD
            keyAlias TRUCK_MUNCHER_DEBUG_KEY_ALIAS
            keyPassword TRUCK_MUNCHER_DEBUG_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            manifestPlaceholders = [googleMapsKey: TRUCK_MUNCHER_GOOGLE_MAPS_KEY]
            testCoverageEnabled false   // Incompatible with Robolectric
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            manifestPlaceholders = [googleMapsKey: "AIzaSyD9O-7TbkrDzHp9tboo4Jx4v5FVE4JSE4Q"]
            testCoverageEnabled false   // Incompatible with Robolectric
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        production {
            buildConfigField "String", "API_ENDPOINT", "\"https://api.truckmuncher.com:8443\""
            versionName "${versionMajor}.${versionMinor}"
        }
        dev {
            buildConfigField "String", "API_ENDPOINT", "\"http://truckmuncher:8443\""
            versionName "${versionMajor}.${versionMinor}.dev"
        }

        // Temporarily disabled. Need either Kotlin delegate functions or Dagger 1
//        mock {
//            buildConfigField "String", "API_ENDPOINT", "\"mock://\""
//            versionName "${versionMajor}.${versionMinor}.${versionPatch}.mock"
//        }
    }

    jacoco {
        version = '0.7.2.201409121644'
    }

    dexOptions {
        // preDexing gives no advantage on CI since we do a clean build
        preDexLibraries = !onCI
    }

    lintOptions {
        enable 'IconExpectedSize', 'StopShip', 'TypographyQuotes'
        disable 'InvalidPackage'
        abortOnError true
        ignoreWarnings false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
}

play {
    serviceAccountEmail = TRUCK_MUNCHER_PLAY_EMAIL_ADDRESS
    pk12File = file(TRUCK_MUNCHER_PLAY_PK12_FILE_PATH)
    uploadImages = true
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName 'com.truckmuncher.app'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.google.android.gms:play-services-maps:7.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.3'
    compile project(':api')

    // Network and Data stack
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'

    // Logging
    compile 'com.jakewharton.timber:timber:2.7.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }

    // UI Utils
    compile 'com.volkhart:android-util:1.0.0-SNAPSHOT@aar'
    compile 'com.squareup.phrase:phrase:1.0.3'
    compile 'com.jakewharton:butterknife:6.0.0'

    // UI Components
    compile 'com.twotoasters.SectionCursorAdapter:library:1.0.1'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.3@aar'

    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile project(':minified-guava')

    compile('com.twitter.sdk.android:twitter:1.3.2@aar') {
        transitive = true;
    }
    compile 'com.facebook.android:facebook-android-sdk:3.23.1'

    compile 'info.metadude.android:typed-preferences:2.0.0'

    debugCompile 'com.squareup.retrofit:retrofit-mock:1.9.0'

    androidTestCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger'
    }
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.3.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    androidTestCompile 'org.mockito:mockito-core:1.10.8'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testCompile project(':testlib')
    testCompile 'org.mockito:mockito-core:1.10.8'
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}
